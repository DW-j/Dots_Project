class Dot{
 
  PVector position, velocity, acceleration;
  PVector[] directions;
  int steps = 0;
  boolean finished = false, reachedGoal = false;
  float fitness;
  boolean bestDot = false;

  Dot(){
    position = new PVector(width/2, height-40);
    velocity = new PVector(0, 0);
    acceleration = new PVector(0, 0);
    
    directions = new PVector[1000];
    for(int i=0; i<directions.length; i++){
      float randomAngle = random(2*PI);
      directions[i] = PVector.fromAngle(randomAngle);
    }
  }
  
  void show(){
     fill(0);
     ellipse(position.x, position.y, 4, 4);
  }
  
  void move(){
    if(steps<directions.length){
      acceleration = directions[steps];
      steps++;
    }else{
      finished = true;
    }
    
    velocity.add(acceleration);
    velocity.limit(15);
    position.add(velocity);
  }
  
  void update(){
    if(!finished){
      move();
      if(dist(position.x, position.y, goal.x, goal.y)<4){
        finished = true;
        reachedGoal = true;
      }else if(position.x<0||position.x>width||position.y<0||position.y>height){
          finished = true;
        }else{
          for(int i=0; i<obstacles.length; i++){
            if(obstacles[i].touchesDot(this)){
              finished = true;
            }
          }
      }
    }
  }
  
  void calculateFitness(){
    if(reachedGoal){
      fitness = 1.0/16.0 + 10000.0/(float)(steps * steps);
    }else{
      float goalDistance = dist(position.x, position.y, goal.x, goal.y);
      fitness = 1/(goalDistance*goalDistance);
    }
  }
  
  void mutate(){
    float mutationRate = 0.01;
    for(int i=0; i<directions.length; i++){
      float rand = random(1);
      if(rand<mutationRate){
        float randomAngle = random(2*PI);
         directions[i] = PVector.fromAngle(randomAngle); 
      }
    }
  }
  
  Dot getChild(){
    Dot child = new Dot();
    for(int i=0; i<directions.length; i++){
      child.directions[i] = directions[i];
    }
    return child;
  }

}
